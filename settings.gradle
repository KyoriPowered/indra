pluginManagement {
  repositories {
    mavenCentral()
    maven {
      url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      name = "s01Snapshots"
      mavenContent { snapshotsOnly() }
    }
    gradlePluginPortal()
  }
}

plugins {
  id "org.gradle.toolchains.foojay-resolver-convention" version "0.8.0"
}

dependencyResolutionManagement {
  repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
  pluginManagement.repositories.each(repositories.&add)
}

rootProject.name = "indra"

def requiredRuntime = 11
if (JavaVersion.current() < JavaVersion.toVersion(requiredRuntime)) {
  throw new GradleException("""\
  You are running a Java version that is too old to build Indra.

  Required: ${requiredRuntime}
  Current: ${JavaVersion.current()}
  """.stripIndent())
}

[
  "indra-common",
  "indra-git",
  "indra-publishing-gradle-plugin",
  "indra-publishing-sonatype",
  "indra-licenser-spotless",
  "indra-licenser-cadix",
  "indra-testlib",
  "indra-crossdoc"
].each {
  include(it)
}
