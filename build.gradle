import net.kyori.indra.IndraExtension
import net.kyori.indra.gradle.IndraPluginPublishingExtension

buildscript {
    ext {
        indraVersion = "2.1.1"
    }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.gradle-plugin" version "$indraVersion" apply false
  id "com.gradle.plugin-publish" version "0.21.0" apply false
  id "com.github.ben-manes.versions" version "0.42.0"
  id "com.diffplug.eclipse.apt" version "3.37.2" apply false
  id "eclipse"
}

group = "net.kyori"
version = "2.1.2-SNAPSHOT"
description = "KyoriPowered organizational build standards and utilities"

subprojects {
  apply plugin: "java-gradle-plugin"
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.license-header"
  apply plugin: "com.diffplug.eclipse.apt"
  
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
  
  dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
  }

  
  if (!project.findProperty('indra.skipPublishing')) {
    apply plugin: "com.gradle.plugin-publish"
    apply plugin: "net.kyori.indra.publishing.gradle-plugin"

    indraPluginPublishing {
      bundleTags("kyori", "standard")
      website("https://github.com/KyoriPowered/indra/wiki")
    }
  }
  
  if ('true'.equals(project.findProperty("skipFunctionalTests"))) {
      tasks.named('test') {
          useJUnitPlatform {
              excludeTags 'functional'
          }
      }
  }
    

  license {
    exclude { it.file.absolutePath.startsWith(project.file('src/test/resources').absolutePath) }
  }

  indra {
    github("KyoriPowered", "indra") {
      ci true
    }
    mitLicense()
    javaVersions().testWith(8, 11, 17)

    configurePublications {
      pom {
        organization {
          name = "KyoriPowered"
          url = "https://kyori.net"
        }

        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
          developer {
            id = "zml"
            email = "zml at kyori [.] net"
            timezone = "America/Vancouver"
          }
        }
      }

      versionMapping {
        usage(Usage.JAVA_API) { fromResolutionOf(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME) }
        usage(Usage.JAVA_RUNTIME) { fromResolutionResult() }
      }
    }

    publishSnapshotsTo("stellardrift", "https://repo.stellardrift.ca/repository/snapshots/")
    
  }
}
