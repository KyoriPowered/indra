plugins {
  id "org.jetbrains.kotlin.jvm"
}

kotlin {
  coreLibrariesVersion = "1.3.21"
  target {
    compilations.configureEach {
      kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.3"
      }
    }
  }
}

sourceSets {
  kotlinSupport
}

tasks.named('jar').configure {
  from(sourceSets.kotlinSupport.output)
}

pluginUnderTestMetadata {
  // Use a packaged form of our plugin to support our classpath hackery
  // def oldFrom = pluginClasspath.from
  pluginClasspath.from = tasks.named('jar').map { it.outputs }
  pluginClasspath.from sourceSets.main.runtimeClasspath
}

dependencies {
  compileOnlyApi "org.immutables:value:2.9.0:annotations"
  annotationProcessor "org.immutables:value:2.9.0"
  compileOnlyApi "org.immutables:builder:2.9.0"
  compileOnlyApi "org.jetbrains:annotations:23.0.0"
  api project(":indra-git")
  implementation "de.jjohannes.gradle:missing-metadata-guava:31.1.1" // https://github.com/jjohannes/missing-metadata-guava
  implementation "gradle.plugin.org.cadixdev.gradle:licenser:0.6.1"
  implementation "org.ow2.asm:asm:9.3"
  api "net.kyori:mammoth:$mammothVersion"
  testImplementation project(":indra-testlib")
  
  // Kotlin plugin dep
  kotlinSupportCompileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
  kotlinSupportCompileOnly(gradleApi())
  compileOnly(sourceSets.kotlinSupport.output)
}

indraPluginPublishing {
  plugin(
    "indra",
    "net.kyori.indra.IndraPlugin",
    "Indra",
    "Simplified tools for configuring modern JVM projects",
    ["boilerplate", "java", "jvm"]
  )

  plugin(
    "indra.checkstyle",
    "net.kyori.indra.IndraCheckstylePlugin",
    "Indra Checkstyle",
    "Checkstyle configuration in line with the Indra file layout",
    ["boilerplate", "checkstyle"]
  )

  plugin(
    "indra.license-header",
    "net.kyori.indra.IndraLicenseHeaderPlugin",
    "Indra License Header",
    "License header configuration in line with the Indra file layout",
    ["boilerplate", "license", "license-header", "licensing"]
  )

  plugin(
    "indra.publishing",
    "net.kyori.indra.IndraPublishingPlugin",
    "Indra Publishing",
    "Reasonable publishing configuration and repository aliases",
    ["boilerplate", "publishing", "nexus"]
  )
}